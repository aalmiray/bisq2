buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.openjfx:javafx-plugin:0.0.10'
        classpath "com.palantir.gradle.gitversion:gradle-git-version:0.12.3"
    }
}

def requiredJavaVersion = 16
def javafxSdkProperty = 'bisq.javafx.sdk'

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: "com.palantir.git-version"

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.31'
        implementation 'ch.qos.logback:logback-core:1.2.3'
        implementation 'ch.qos.logback:logback-classic:1.2.3'
        testImplementation "com.google.truth:truth:1.1.3"
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(requiredJavaVersion)
        }
    }
}

configure(project(':api')) {
    apply plugin: 'java-library'
}

configure(project(':client')) {
    apply plugin: 'java-library'

    dependencies {
        api project(':api')
        implementation 'com.squareup.okhttp3:okhttp:4.9.1'
        implementation 'com.google.code.gson:gson:2.8.7'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

configure(project(':core')) {
    apply plugin: 'java-library'

    dependencies {
        api project(':api')
        implementation 'com.sparkjava:spark-core:2.9.3'
        implementation 'com.google.code.gson:gson:2.8.7'
    }
}

configure(project(':app')) {
    apply plugin: 'java-library'

    dependencies {
        api 'info.picocli:picocli:4.6.1'
    }
}

configure(project(':app:daemon')) {
    apply plugin: 'application'

    dependencies {
        implementation project(':app')
        implementation project(':client')
        implementation project(':core')
    }

    application {
        mainClass = 'bisq.daemon.app.Main'
        applicationName = 'bisqd'
    }

    processResources {
        expand(name: applicationName, version: gitVersion())
    }

    build.dependsOn installDist
}

configure(project(':app:cli')) {
    apply plugin: 'application'

    dependencies {
        implementation project(':app')
        implementation project(':client')
        implementation 'com.google.code.gson:gson:2.8.7'
        testImplementation project(':core')
    }

    application {
        mainClass = 'bisq.app.cli.Main'
        applicationName = 'bisq'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    // see https://picocli.info/autocomplete.html
    task generateBashCompletion(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        mainClass = 'picocli.AutoComplete'
        args 'bisq.app.cli.BisqCommand',
                '--force',
                "--completionScript=${project.rootDir}/bisq-completion.bash"
        javaLauncher = javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(requiredJavaVersion)
        }
    }

    processResources {
        expand(name: "bisq cli", version: gitVersion())
    }

    build.dependsOn installDist, generateBashCompletion
}

configure(project(':app:fx')) {
    apply plugin: 'application'
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        if (project.hasProperty(javafxSdkProperty))
            sdk = project.getProperty(javafxSdkProperty)
        else
            version = '16'
        modules = ['javafx.controls']
    }

    application {
        mainClass = 'bisq.app.fx.Main'
        applicationName = 'bisqfx'
    }

    dependencies {
        implementation project(':app')
        implementation project(':client')
        implementation project(':core')
    }

    startScripts {
        defaultJvmOpts = [ '--add-modules=javafx.controls' ]
        if (project.hasProperty(javafxSdkProperty))
            defaultJvmOpts += "--module-path=${project.getProperty(javafxSdkProperty)}/lib"
    }

    // see https://picocli.info/autocomplete.html
    task generateBashCompletion(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        mainClass = 'picocli.AutoComplete'
        args 'bisq.app.fx.BisqFXCommand',
            '--force',
            "--completionScript=${project.rootDir}/bisqfx-completion.bash"
        javaLauncher = javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(requiredJavaVersion)
        }
    }

    processResources {
        expand(name: applicationName, version: gitVersion())
    }

    build.dependsOn installDist, generateBashCompletion
}
