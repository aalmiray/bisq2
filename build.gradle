buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.openjfx:javafx-plugin:0.0.10'
    }
}

configure(subprojects) {
    repositories {
        mavenCentral()
    }
}

configure(project(':api')) {
    apply plugin: 'java-library'

    dependencies {
        api 'io.reactivex.rxjava3:rxjava:3.0.13'
    }
}

configure(project(':core')) {
    apply plugin: 'java'

    dependencies {
        implementation project(':api')
    }
}

configure(project(':http-service')) {
    apply plugin: 'java'

    dependencies {
        implementation project(':api')
        implementation project(':core')
        implementation 'com.sparkjava:spark-core:2.9.3'
        implementation 'com.google.code.gson:gson:2.8.7'
    }
}

configure(project(':http-client')) {
    apply plugin: 'java-library'

    dependencies {
        api project(':api')
        implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    }
}

configure(project(':daemon')) {
    apply plugin: 'application'

    dependencies {
        implementation project(':api')
        implementation project(':core')
        implementation project(':http-service')
    }

    application {
        mainClass = 'bisq.app.daemon.BisqDaemon'
    }

    build.dependsOn installDist
}

configure(project(':cli')) {
    apply plugin: 'application'

    dependencies {
        implementation project(':http-client')
        implementation 'info.picocli:picocli:4.6.1'
        testImplementation project(':daemon')
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    application {
        mainClass = 'bisq.app.cli.BisqCli'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    // see https://picocli.info/autocomplete.html
    task generateBashCompletion(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        mainClass = 'picocli.AutoComplete'
        args 'bisq.app.cli.BisqCli$Bisq', '--force', '--completionScript=../bisq-completion.bash'
    }

    build.dependsOn installDist, generateBashCompletion
}

configure(project(':desktop')) {
    apply plugin: 'application'
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        //version = '17-ea+11'
        sdk = '/Users/cbeams/lab/javafx-sdk/javafx-sdk-17/'
        modules = ['javafx.controls']
    }

    application {
        mainClass = 'bisq.app.desktop.BisqDesktop'
    }

    dependencies {
        implementation project(':http-client')
    }

    startScripts {
        defaultJvmOpts = [
                '--module-path=/Users/cbeams/lab/javafx-sdk/javafx-sdk-17/lib',
                '--add-modules=javafx.controls'
        ]
    }

    build.dependsOn installDist
}
